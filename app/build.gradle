apply plugin: 'com.android.application'

android {
    compileSdkVersion 24
    buildToolsVersion "24.0.3"

    defaultConfig {
        applicationId "ru.ifmo.neerc.chat.android"

        minSdkVersion 14

        versionCode 10
        versionName "0.3.3"
    }

    productFlavors {
        neerc {

        }

        gplay {
            applicationIdSuffix ".gplay"
        }
    }

    signingConfigs {
        productFlavors.each { flavor ->
            def keystorePropertiesFile = rootProject.file("${flavor.name}.properties")

            if (keystorePropertiesFile.exists()) {
                def keystoreProperties = new Properties()
                keystoreProperties.load(new FileInputStream(keystorePropertiesFile))

                "${flavor.name}" {
                    storeFile rootProject.file(keystoreProperties['storeFile'])
                    storePassword keystoreProperties['storePassword']
                    keyAlias keystoreProperties['keyAlias']
                    keyPassword keystoreProperties['keyPassword']
                }

                flavor.signingConfig = signingConfigs[flavor.name]
            }
        }
    }
}

dependencies {
    compile 'com.android.support:appcompat-v7:24.2.1'
    compile 'com.android.support:recyclerview-v7:24.2.1'
    compile 'com.android.support:design:24.2.1'

    compile 'com.google.firebase:firebase-core:10.0.1'
    compile 'com.google.firebase:firebase-crash:10.0.1'

    compile 'org.igniterealtime.smack:smack-android:4.1.5'
    compile 'org.igniterealtime.smack:smack-tcp:4.1.5'
    compile 'org.igniterealtime.smack:smack-extensions:4.1.5'
    compile 'org.jxmpp:jxmpp-core:0.5.0-alpha7'

    compile project(':shared')
}

apply plugin: 'com.google.gms.google-services'
