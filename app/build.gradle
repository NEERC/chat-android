apply plugin: 'com.android.application'

android {
    compileSdkVersion 27

    defaultConfig {
        applicationId "ru.ifmo.neerc.chat.android"

        minSdkVersion 14

        versionCode 17
        versionName "0.4.1"
    }

    flavorDimensions "tier"

    productFlavors {
        neerc {

        }

        gplay {
            applicationIdSuffix ".gplay"
        }
    }

    signingConfigs {
        productFlavors.each { flavor ->
            def keystorePropertiesFile = rootProject.file("${flavor.name}.properties")

            if (keystorePropertiesFile.exists()) {
                def keystoreProperties = new Properties()
                keystoreProperties.load(new FileInputStream(keystorePropertiesFile))

                "${flavor.name}" {
                    storeFile rootProject.file(keystoreProperties['storeFile'])
                    storePassword keystoreProperties['storePassword']
                    keyAlias keystoreProperties['keyAlias']
                    keyPassword keystoreProperties['keyPassword']
                }

                flavor.signingConfig = signingConfigs[flavor.name]
            }
        }
    }
}

configurations {
    all*.exclude group: 'xpp3', module: 'xpp3'
}

dependencies {
    implementation 'com.android.support:appcompat-v7:27.0.0'
    implementation 'com.android.support:recyclerview-v7:27.0.0'
    implementation 'com.android.support:design:27.0.0'

    implementation 'com.google.firebase:firebase-core:11.6.0'
    implementation 'com.google.firebase:firebase-crash:11.6.0'
    implementation 'com.google.firebase:firebase-messaging:11.6.0'

    implementation 'org.igniterealtime.smack:smack-android:4.2.0'
    implementation 'org.igniterealtime.smack:smack-tcp:4.2.0'
    implementation 'org.igniterealtime.smack:smack-extensions:4.2.0'
    implementation 'org.igniterealtime.smack:smack-experimental:4.2.0'

    implementation 'com.bugfender.sdk:android:0.+'

    implementation project(':shared')
}

apply plugin: 'com.google.gms.google-services'
